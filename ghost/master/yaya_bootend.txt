//******************************************************************************
// あやりりすEX　ゴーストテンプレート
// 起動/終了トーク辞書
//******************************************************************************

//******************************************************************************
//ウィンドウを一体化
//******************************************************************************
//\0と\1の位置を同期
一体化
{
 	"\1\![set,alignmenttodesktop,free]\![move,0,0,0,0,left.top,left.top]\![set,sticky-window,1,0]"
}


//******************************************************************************
//初回起動 OnFirstBoot
//******************************************************************************

//---- Vanish（消滅）カウントを見て、真の初回か否かを判定する
初回起動
{
	一体化
	--
	AYLex_DEFAULT_SURFACE[0] = 3
	
	起動回数 = 0
	rank = 0 //ランク
	ru_flag = 0 //ランクアップフラグ
	flag_talk = 0 //フラグトークを見たかどうか
	tell_name = 0 //名前を教えたかどうか
	//---- ほんとうに初めての起動
	<<"
	\t\0\s[-1]\w9
	ユ：……今日の仕事は散々だったな。
	疲れたし、公園にでも寄って休憩していこうか。
	改行多めユ：人気もなくて、作業も捗りそうだ。\w9
	クリック待ちユ：適当なベンチに腰掛けて、ノートPCを取り出す。
	改行多めユ：俺の他には、大学生くらいの男の子が一人だけ、
	物憂げに池の方を眺めている。
	\0\s[3]\w9
	ユ：なんだか、ミステリアスな雰囲気だな。\w9
	真2：…………？\w9
	クリック待ちユ：（……目が、合ってしまった）\w9
	真0：……どうも。
	=====
	真3：\t…………。\w9
	ユ：(医学が進歩して、単眼の人も長生きできるように\nなったのは、何十年も前のことだったか。
	改行多めユ：話には聞いたことがあったが……
	目にするのは初めてだな)
	クリック待ちユ：(物珍しさに、思わずチラチラと見てしまう。
	改行多めユ：…いかん。仕事だ、仕事)
	">>

}

Vanish後初回起動
{
	_vanish_count = TOINT(reference[0])

	//---- 再インストール後の初回起動
	if _vanish_count >= 1
	{
		一体化
		--
		起動回数 = 0
		rank = 1 //ランクC開始
		ru_flag = 0 //ランクアップフラグ
		tell_name = 0 //名前を教えたかどうか
		<<"
		真4：あ、(ユーザ)さん…！
		久しぶりだね。
		改行多め真0：会いたかったよ。
		改行多め真7：…なんちゃって。
		真0：また、よろしくね。
		">>
	}
}

//******************************************************************************
//通常の起動 OnBoot
//******************************************************************************
//該当がない時は「起動」でトークします。
起動
{
	起動回数 ++
	一体化
	--
	if rank == 1 {AYLex_DEFAULT_SURFACE[0] = 2}
	if rank == 0 {
		AYLex_DEFAULT_SURFACE[0] = 3
		"\0\s[3]…………。\1あ、今日もいるな。\n何してるんだろう。\e"
	} else {
		"\0\s[0]"
		--
		"また会ったね。"
		_now = GetTimeSlotList
		case _now {
			when "朝" {"おはよー、"}
			when "昼" {"こんにちは、"}
			when "夕方", "夜" {"こんばんは、"}
			when "深夜" {"いい時間だね、"}
			others {"こんにちは、"}
		}
		--
		"(ユーザ)さん。\w9\n\n[half]"
		--
		if rank == 3 {"…そろそろ来るかなって、思ってたんだ。"}
		"\s[0]お仕事お疲れさま。"
		"\s[0]…コーヒーでも、買ってこようか。"
		"\s[1]今日は天気がよくて、気持ちいいよ。"
		"…たまには、展望台にでも行ってみる？"
		--
		"\e"
	}
}

//******************************************************************************
//終了 OnClose
//******************************************************************************
//普通は時間帯別の挨拶
終了 : nonoverlap
{
	if rank == 0 {"\0\s[3]…………。\1そろそろ帰るか。"}
	else {
		"\0\s[0]帰るんだね。\w9"
		"\0\s[0]お疲れさま。\w9"
		_now = GetTimeSlotList
		case _now {
			when "朝","昼" {"\s[1]俺は、もうちょっとゆっくりしていこうかな。\w9"}
			when "夕方","夜" {"\s[1]俺もそろそろ帰ろうかな。\w9"}
			when "深夜" {"\s[0]もう寝るのかな。\w9\s[6]おやすみ。\w9"}
		}
		--
		"\w9\n\n[half]"
		--
		"\s[0]…また、\w5会えるといいんだけど。\w9"
		"\s[0]…良かったら、\w5また来てよ。\w9"
	}
}

//******************************************************************************
//最小化からの復帰 OnWindowStateRestore
//******************************************************************************
OnWindowStateRestore : nonoverlap
{
	if rank == 0 {"\0\s[3]…………。"}
	else {
		<<"
		真：ただいま。コーヒー買ってきたよ。
		-----
		真：おかえり。どこ行ってたの？
		">>
	}
}
